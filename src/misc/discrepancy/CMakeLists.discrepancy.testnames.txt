# $Id$

NCBI_begin_custom_target(discrepancy.testnames)
    NCBI_custom_target_definition(define_discrepancy_testnames_target)
NCBI_end_custom_target()

function(define_discrepancy_testnames_target variable access value current_list_file stack)

    get_filename_component(_source_dir ${current_list_file} DIRECTORY)
    file(RELATIVE_PATH _rel_path ${NCBI_SRC_ROOT} ${_source_dir})
    set(_target_dir ${NCBI_INC_ROOT}/${_rel_path})
    # intermediate location at inc folder
    set(_target_dir_int ${NCBI_CFGINC_ROOT}/${_rel_path})

    set(_cpp_files)

    foreach(_module ${_discrepancy_modules})
       list(APPEND _cpp_files ${_module}.cpp)
    endforeach()

    list(JOIN _cpp_files " " _cpp_files_joined)

    add_custom_command(
        OUTPUT  ${_target_dir}/testnames.inc ${_target_dir_int}/testnames.int
        COMMAND ${CMAKE_COMMAND} -E make_directory ${_target_dir_int}
        COMMAND ${CMAKE_COMMAND} -P ${_source_dir}/test_names.cmake ${_target_dir_int}/testnames.int "${_cpp_files_joined}"
        COMMAND ${CMAKE_COMMAND} -E copy_if_different ${_target_dir_int}/testnames.int ${_target_dir}/testnames.inc
        COMMENT "Running make_test_names.py for modules: ${_discrepancy_modules}"
        WORKING_DIRECTORY ${_source_dir}
        DEPENDS ${current_list_file} ${_cpp_files} ${_source_dir}/CMakeLists.txt ${_source_dir}/test_names.cmake
        VERBATIM
        )

    add_custom_target(
        discrepancy.testnames
        DEPENDS  ${_target_dir}/testnames.inc
        SOURCES ${current_list_file} ${_source_dir}/CMakeLists.txt ${_source_dir}/test_names.cmake
    )

endfunction()
